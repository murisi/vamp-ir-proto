WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "%" ~ (!"\n" ~ ANY)* }
program = _{ SOI ~ statement* ~ EOI }
statement = { assertion | query }
assertion = { clause ~ "." }
query = { predicate ~ "?" }
clause = { predicate ~ (":-" ~ body)? }
body = { literal ~ ("," ~ literal)* }
literal = { expression ~ relOp ~ expression | predicate }
relOp = { "=" | "!=" }
predicate = { predicate_sym ~ ("(" ~ terms? ~ ")")? }
predicate_sym = { identifier }
terms = { term ~ ("," ~ term)* }
term = { constant | variable }
expression = { product ~ (plus_minus ~ product)* }
product = { value ~ (times_divide ~ value)* }
value = { term | "(" ~ expression ~ ")" | negate ~ value }
negate = { "-" }
times_divide = { "*" | "/" }
plus_minus = { "+" | "-" }
constant = { integer }
integer = @{ "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
char = { ASCII_ALPHANUMERIC | "_" }
variable = @{ char+ }
identifier = @{ char+ }
